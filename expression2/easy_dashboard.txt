@name Easy Dashboard
@inputs [Parent Base]:entity RPM Gear Fuel MaxFuel
@outputs Mileage Dist
@persist Scale Pos:vector Ang:angle Kph Dist Mileage Mileage_Instance:string
@trigger 

if(first() | dupefinished()){
    Scale = 0.6
    Pos = vec(2, -0.5, 0)
    Ang = ang(0, 0, -90)
    
    Mileage_Instance = "trike_2023_12_26"
    fileLoad("car_mileage/"+Mileage_Instance+".txt")
    
    holoCreate(1, Parent:toWorld(Pos + vec(0, 4.2, 0):rotate(Ang) * Scale), vec(Scale), Parent:toWorld(Ang), vec(150), "models/sprops/trans/misc/gauge_2.mdl")
    holoSkin(1, 1)
    holoParent(1, Parent)    
    
    holoCreate(2, Parent:toWorld(Pos + vec(0, -4.2, 0):rotate(Ang) * Scale), vec(Scale), Parent:toWorld(Ang), vec(150), "models/sprops/trans/misc/gauge_2.mdl")
    holoParent(2, Parent)    
    
    holoCreate(3, Parent:toWorld(Pos + vec(0, 0, 5.5):rotate(Ang) * Scale), vec(1, 0.6, 0.6) * Scale, Parent:toWorld(Ang), vec(150), "models/sprops/trans/misc/gauge_2.mdl")
    holoSkin(3, 2)
    holoParent(3, Parent)   
    
    #Gear gizmo
    holoCreate(4, holoEntity(3):toWorld(vec(-0.7, 0, -0.8) * Scale), vec(0.25) * Scale, holoEntity(3):toWorld(ang(36 * Gear, 0, 0)), vec(255), "models/sprops/misc/numberwheel.mdl")
    holoParent(4, Parent)
    
    #RPM dial
    holoCreate(5, holoEntity(1):toWorld(vec(0.4, 0, 0) * Scale), vec(0.1) * Scale, holoEntity(1):toWorld(ang(90 + RPM * 0.036, 90, 90)), vec(50), "hq_cylinder")
    holoParent(5, Parent)    
    
    holoCreate(6, holoEntity(5):toWorld(vec(1.5, 0, 0.2) * Scale), vec(0.05, 0.05, 0.25) * Scale, holoEntity(5):toWorld(ang(0, 90, 90)), vec(255), "prism")
    holoDisableShading(6, 1)
    holoParent(6, 5)
    
    #Speedo dial
    holoCreate(7, holoEntity(2):toWorld(vec(0.4, 0, 0) * Scale), vec(0.1) * Scale, holoEntity(2):toWorld(ang(90, 90, 90)), vec(50), "hq_cylinder")
    holoParent(7, Parent)    
    
    holoCreate(8, holoEntity(7):toWorld(vec(1.5, 0, 0.2) * Scale), vec(0.05, 0.05, 0.25) * Scale, holoEntity(7):toWorld(ang(0, 90, 90)), vec(255), "prism")
    holoDisableShading(8, 1)
    holoParent(8, 7)
    
    #Fuel dial
    holoCreate(9, holoEntity(3):toWorld(vec(0.8, 0, 0) * Scale), vec(0.05) * Scale, holoEntity(3):toWorld(ang(129 + (281 * (Fuel / MaxFuel)), 90, 90)), vec(50), "hq_cylinder")
    holoParent(9, Parent)    
    
    holoCreate(10, holoEntity(9):toWorld(vec(1.2, 0, 0) * Scale), vec(0.02, 0.02, 0.15) * Scale, holoEntity(9):toWorld(ang(0, 90, 90)), vec(255), "prism")
    holoDisableShading(10, 1)
    holoParent(10, 9)
    
    #Mileage display
    for(I = 0, 4){
        holoCreate(11 + I, holoEntity(2):toWorld(vec(-0.15, 0.9 - (I * 0.5), -1.25) * Scale), vec(0.15) * Scale, holoEntity(2):toWorld(ang()), vec(255), "models/sprops/misc/numberwheel.mdl")
        holoParent(11 + I, Parent)
    }
}

event fileLoaded(FilePath:string, Data:string) {
    Mileage = Data:toNumber()
    Dist = Data:toNumber()
    
    for(I = 0, 4){
        local Math = floor(Dist / (10 ^ I))  * 36
        if(!Math){ continue }
            
        holoAng(11 + I, holoEntity(2):toWorld(ang(Math, 0, 0)))
    }
}

if(~Gear){
    holoAng(4, holoEntity(3):toWorld(ang(36 * Gear, 0, 0)))
}
if(~RPM){
    holoAng(5, holoEntity(1):toWorld(ang(90 + RPM * 0.036, 90, 90)))
}

if(~Fuel){
    holoAng(9, holoEntity(3):toWorld(ang(129 + (281 * (Fuel / MaxFuel)), 90, 90)))
}
event tick(){
    #Speedometer
    Kph = min(toUnit("km/h", abs(Base:velL():x())), 180)
    if(changed(Kph)){
        holoAng(7, holoEntity(2):toWorld(ang(90 + Kph * 1.77, 90, 90)))
    }
    
    #Mileage
    Mileage += (Kph * 0.00027777777777778) * tickInterval()
    Dist = floor(Mileage)
    if(changed(Dist)){
        for(I = 0, 4){
            local Math = floor(Dist / (10 ^ I))  * 36
            if(!Math){ continue }
            
            holoAng(11 + I, holoEntity(2):toWorld(ang(Math, 0, 0)))
        }
        #Save mileage to a file
        fileWrite("car_mileage/"+Mileage_Instance+".txt", toString(Dist)) 
    }
}

event removed(_:number){
   fileWrite("car_mileage/"+Mileage_Instance+".txt", toString(Mileage))  
}
